apply plugin: "com.android.application"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs"

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://maven.fabric.io/public" }
    maven { url "https://jitpack.io" }
    maven { url "http://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'http://storage.googleapis.com/r8-releases/raw' }
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
    mapDiagnosticLocations = true
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
        arg("moshi.generated", "javax.annotation.Generated")
    }
    javacOptions {
        option("-Xmaxerrs", 1000)
    }
}

android {
    compileSdkVersion 28


    defaultConfig {
        applicationId "com.topjohnwu.magisk"
        versionName rootProject.ext.configProps['appVersion']
        versionCode rootProject.ext.configProps['appVersionCode'] as Integer

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    dataBinding {
        enabled = true
    }

}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")
    implementation project(":net")
    implementation project(":shared")
    implementation project(":signing")


    // DEX
    def vDex = "1.0.3"
    implementation group: "com.android.support", name: "multidex", version: vDex

    // FRAMEWORK
    def vTeanity = "0.3"
    implementation group: "com.github.skoumalcz", name: "teanity", version: vTeanity

    // KOTLIN
    def vKotlin = "1.3.21"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk7", version: vKotlin

    // SUPPORT LIBS
    def vMaterial = "1.1.0-alpha04"
    def vConstraint = "2.0.0-alpha3"
    def vRoom = "2.1.0-alpha05"
    def vWork = "2.0.0"
    def vPreference = "1.1.0-alpha04"
    implementation group: "com.google.android.material", name: "material", version: vMaterial
    implementation group: "androidx.constraintlayout", name: "constraintlayout", version: vConstraint
    implementation group: "androidx.room", name: "room-rxjava2", version: vRoom
    implementation group: "androidx.work", name: "work-runtime-ktx", version: vWork
    implementation group: "androidx.preference", name: "preference", version: vPreference, ext: "aar"

    // DEPENDENCY INJECTION
    def vKoin = "2.0.0-beta-1"
    implementation group: "org.koin", name: "koin-core", version: vKoin
    implementation group: "org.koin", name: "koin-android", version: vKoin
    implementation group: "org.koin", name: "koin-androidx-viewmodel", version: vKoin

    // NETWORKING
    def vRetrofit = "2.5.0"
    def vOkHttp = "3.12.0"
    def vMoshi = "1.8.0"
    implementation group: "com.squareup.retrofit2", name: "retrofit", version: vRetrofit
    implementation group: "com.squareup.retrofit2", name: "converter-moshi", version: vRetrofit
    implementation group: "com.squareup.retrofit2", name: "adapter-rxjava2", version: vRetrofit
    implementation group: "com.squareup.okhttp3", name: "okhttp", version: vOkHttp
    implementation group: "com.squareup.okhttp3", name: "logging-interceptor", version: vOkHttp
    implementation group: "com.squareup.moshi", name: "moshi", version: vMoshi
    implementation group: "com.squareup.moshi", name: "moshi-kotlin", version: vMoshi

    // GLIDE
    def vGlide = "4.9.0"
    implementation group: "com.github.bumptech.glide", name: "okhttp3-integration", version: vGlide, ext: "aar"

    // EASING
    def vKotpref = "2.6.0"
    def vTimber = "4.7.1"
    def vJTAR = "2.3"
    implementation group: "com.chibatching.kotpref", name: "kotpref", version: vKotpref
    implementation group: "com.jakewharton.timber", name: "timber", version: vTimber
    implementation group: "org.kamranzafar", name: "jtar", version: vJTAR

    // UI
    def vOSS = "16.0.2"
    def vTransformsGlide = "4.0.0"
    def vTransformsRecycler = "3.0.0"
    def vSnap = "2.0"
    implementation group: "com.google.android.gms", name: "play-services-oss-licenses", version: vOSS
    implementation group: "jp.wasabeef", name: "glide-transformations", version: vTransformsGlide
    implementation group: "jp.wasabeef", name: "recyclerview-animators", version: vTransformsRecycler
    implementation group: "com.github.rubensousa", name: "gravitysnaphelper", version: vSnap

    // SU
    def vSU = '2.3.2'
    implementation group: "com.github.topjohnwu.libsu", name: "core", version: vSU
    implementation group: "com.github.topjohnwu.libsu", name: "io", version: vSU

    // KAPTs
    def vState = "1.4.1"
    kapt group: "com.squareup.moshi", name: "moshi-kotlin-codegen", version: vMoshi
    kapt group: "com.github.bumptech.glide", name: "compiler", version: vGlide
    kapt group: "com.evernote", name: "android-state-processor", version: vState
    kapt group: "androidx.room", name: "room-compiler", version: vRoom
}
